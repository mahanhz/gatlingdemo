buildscript {
	ext {
		springBootVersion = '2.0.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id "com.github.lkishalmi.gatling" version "0.7.3"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

configure(allprojects) {
	apply plugin: 'java'

	sourceCompatibility = JavaVersion.VERSION_1_8

	repositories {
		mavenCentral()
	}
}

project("test-data-service") {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	dependencies {
		compile('org.springframework.boot:spring-boot-starter-webflux')
		testCompile('org.springframework.boot:spring-boot-starter-test')
		testCompile('io.projectreactor:reactor-test')
	}
}

project("test-data-provider") {
	apply plugin: 'io.spring.dependency-management'

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
		}
	}

	dependencies {
		compile('org.springframework.boot:spring-boot-starter-webflux')
		testCompile('org.springframework.boot:spring-boot-starter-test')
		testCompile('io.projectreactor:reactor-test')
	}
}

project("load-test") {
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'com.github.lkishalmi.gatling'

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
		}

		gatling {
			dependencies {
				dependencySet(group: 'io.netty', version: '4.0.51.Final') {
					entry 'netty-codec-http'
					entry 'netty-codec'
					entry 'netty-handler'
					entry 'netty-buffer'
					entry 'netty-transport'
					entry 'netty-common'
					entry 'netty-transport-native-epoll'
				}
			}
		}
	}

	dependencies {
		//compile project(':test-data-provider')
		gatlingCompile project(':test-data-provider')
	}
}

